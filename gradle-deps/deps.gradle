apply from: "${rootProject.rootDir}/gradle-deps/base.gradle"
project.ext {
    //是否允许module单独调试
    isModuleDebug = false
    moduleName = "main"//单独调试module名  main    Demo
    //基础信息配置
    compileSdkVersion = versions.compileSdk
    buildToolsVersion = versions.buildTools
    minSdkVersion = versions.minSdk
    targetSdkVersion = versions.targetSdk
    versionCode = versions.versionCode
    versionName = versions.versionName

    //设置app配置
    setAppDefaultConfig = { extension ->

        //设置项目的android
        setAppAndroidConfig extension.android
        //设置项目的三方库依赖
        setDependencies extension.dependencies
    }

    //设置application 公共的android配置
    setAppAndroidConfig = { extension ->
        extension.compileSdkVersion project.ext.compileSdkVersion
        extension.buildToolsVersion project.ext.buildToolsVersion
        extension.defaultConfig {
            applicationId project.ext.applicationId
            minSdkVersion project.ext.minSdkVersion
            targetSdkVersion project.ext.targetSdkVersion
            versionCode project.ext.versionCode
            versionName project.ext.versionName
            extension.flavorDimensions "versionCode"
            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [AROUTER_MODULE_NAME: project.getName()]
                }
            }
        }

        extension.compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        extension.kotlinOptions {
            jvmTarget = '1.8'
        }
        extension.buildFeatures.viewBinding = true
    }

    //动态改变，用于单模块调试
    setAppOrLibDefaultConfig = { extension ->
        if (project.ext.isModuleDebug && project.ext.moduleName == project.name) {
            extension.apply plugin: 'com.android.application'
            extension.description "app"
        } else {
            extension.apply plugin: 'com.android.library'
            extension.description "lib"

        }
        extension.apply plugin: 'kotlin-android'
        extension.apply plugin: 'kotlin-parcelize'
        extension.apply plugin: 'kotlin-kapt'

        appImplementation = project.name

        //设置通用Android配置
        setAppOrLibAndroidConfig extension.android
    }

    //设置通用的 android配置（可作为project单独调试）
    setAppOrLibAndroidConfig = { extension ->
        extension.compileSdkVersion project.ext.compileSdkVersion
        extension.buildToolsVersion project.ext.buildToolsVersion
        extension.defaultConfig {
            if (project.ext.isModuleDebug && project.ext.moduleName == project.name) {
                applicationId "com.troll." + project.getName()
            }
            minSdkVersion project.ext.minSdkVersion
            targetSdkVersion project.ext.targetSdkVersion
            versionCode project.ext.versionCode
            versionName project.ext.versionName
            extension.flavorDimensions "versionCode"
            //ARouter 编译生成路由
            javaCompileOptions {
                annotationProcessorOptions {
                    arguments = [AROUTER_MODULE_NAME: project.getName()]
                }
            }
        }

        //使用的jdk版本
        extension.compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }
        extension.kotlinOptions {
            jvmTarget = '1.8'
        }
        //动态改变清单文件资源指向
        extension.sourceSets {
            main {
                if (project.ext.isModuleDebug && project.ext.moduleName == project.name) {
                    manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
                } else {
                    manifest.srcFile 'src/main/AndroidManifest.xml'
                }
            }
        }
        extension.buildFeatures.viewBinding = true

        extension.namespace "${project.getName()}"

    }
}